#:include 'macros.fpp'

!>
!! @file m_helper.f90
!! @brief Contains module m_helper

module m_helper

    use m_derived_types        !< Definitions of the derived types

    use m_global_parameters    !< Definitions of the global parameters

    use ieee_arithmetic        !< For checking NaN

    implicit none

    private; 
    public :: s_comp_n_from_prim, &
              s_comp_n_from_cons, &
              s_initialize_bubbles_model, &
              s_initialize_nonpoly, &
              s_simpson, &
              s_transcoeff, &
              s_int_to_str, &
              s_transform_vec, &
              s_transform_triangle, &
              s_transform_model, &
              s_swap, &
              f_cross, &
              f_create_transform_matrix, &
              f_create_bbox, &
              s_print_2D_array, &
              f_xor, &
              f_logical_to_int, &
              unassociated_legendre, &
              associated_legendre, &
              spherical_harmonic_func, &
              double_factorial, &
              factorial, &
              f_cut_on, &
              f_cut_off

contains

    !> Computes the bubble number density n from the primitive variables
        !! @param vftmp is the void fraction
        !! @param Rtmp is the  bubble radii
        !! @param ntmp is the output number bubble density
    pure subroutine s_comp_n_from_prim(vftmp, Rtmp, ntmp, weights)
        $:GPU_ROUTINE(parallelism='[seq]')
        real(wp), intent(in) :: vftmp
        real(wp), dimension(nb), intent(in) :: Rtmp
        real(wp), intent(out) :: ntmp
        real(wp), dimension(nb), intent(in) :: weights

        real(wp) :: R3

        R3 = dot_product(weights, Rtmp**3._wp)
        ntmp = (3._wp/(4._wp*pi))*vftmp/R3

    end subroutine s_comp_n_from_prim

    pure subroutine s_comp_n_from_cons(vftmp, nRtmp, ntmp, weights)
        $:GPU_ROUTINE(parallelism='[seq]')
        real(wp), intent(in) :: vftmp
        real(wp), dimension(nb), intent(in) :: nRtmp
        real(wp), intent(out) :: ntmp
        real(wp), dimension(nb), intent(in) :: weights

        real(wp) :: nR3

        nR3 = dot_product(weights, nRtmp**3._wp)
        ntmp = sqrt((4._wp*pi/3._wp)*nR3/vftmp)

    end subroutine s_comp_n_from_cons

    impure subroutine s_print_2D_array(A, div)

        real(wp), dimension(:, :), intent(in) :: A
        real(wp), optional, intent(in) :: div

        integer :: i, j
        integer :: local_m, local_n
        real(wp) :: c

        local_m = size(A, 1)
        local_n = size(A, 2)

        if (present(div)) then
            c = div
        else
            c = 1._wp
        end if

        print *, local_m, local_n

        do i = 1, local_m
            do j = 1, local_n
                write (*, fmt="(F12.4)", advance="no") A(i, j)/c
            end do
            write (*, fmt="(A1)") " "
        end do
        write (*, fmt="(A1)") " "

    end subroutine s_print_2D_array

    !> 
          !! bubbles_euler + polytropic
          !! bubbles_euler + non-polytropic
          !! bubbles_lagrange + non-polytropic
    impure subroutine s_initialize_bubbles_model()

        ! Allocate memory for non-polytropic EE bubbles
        if (bubbles_euler) then
          if (.not. polytropic) then
            @:ALLOCATE(pb0(nb), Pe_T(nb))
            @:ALLOCATE(k_n(nb), k_v(nb), mass_n0(nb), mass_v0(nb))
            @:ALLOCATE(Re_trans_T(nb), Re_trans_c(nb), Im_trans_T(nb), Im_trans_c(nb))
          else if (polytropic .and. qbmm) then
            @:ALLOCATE(pb0(nb))
          end if

          ! Compute quadrature weights and nodes for polydisperse simulations
          if (nb > 1) then
              call s_simpson(weight, R0)
          end if
          R0 = R0*(bub_refs%R0ref/bub_refs%x0)

        ! Restrictions on Lagrange bubbles
        else if (bubbles_lagrange) then
            ! Need improvements to accept polytropic gas compression, isothermal 
            ! and adiabatic thermal models, and the Gilmore and RP bubble models.
            ! If Keller-Miksis model is not selected, then no radial motion
            polytropic = .false.    ! Forcing no polytropic model
            thermal = 3             ! Forcing constant transfer coefficient model based on Preston et al., 2007
        end if

        ! Initialize bubble variables
        call s_initialize_bubble_refs()
        call s_initialize_bubble_vars()

    end subroutine s_initialize_bubbles_model

    !>
    impure subroutine s_initialize_bubble_refs()

      if (.not. f_is_default(bub_refs%rho0) .and. f_is_default(bub_refs%rhob0)) then
          bub_refs%rhob0 = bub_refs%rho0
      end if

      if (.not. f_is_default(bub_refs%x0) .and. f_is_default(bub_refs%R0ref)) then
          bub_refs%R0ref = bub_refs%x0
      end if

      if (f_is_default(bub_refs%u0)) then
          bub_refs%u0 = sqrt(bub_refs%p0/bub_refs%rho0)
      else if (f_is_default(bub_refs%p0)) then
          bub_refs%p0 = bub_refs%rho0*bub_refs%u0*bub_refs%u0
      end if

      if (f_is_default(bub_refs%ub0)) then
          bub_refs%ub0 = sqrt(bub_refs%p0eq/bub_refs%rhob0)
      else if (f_is_default(bub_refs%p0eq)) then
          bub_refs%p0eq = bub_refs%rhob0*bub_refs%ub0*bub_refs%ub0
      end if

      if (.not. f_is_default(bub_refs%T0) .and. f_is_default(bub_refs%Thost)) then
          bub_refs%Thost = bub_refs%T0
      end if

    end subroutine s_initialize_bubble_refs


    !> 
    impure subroutine s_initialize_bubble_vars()
        integer :: id_bubbles, id_host
        real(wp) :: rho0, u0, T0, x0, p0, rhob0, p0eq, ub0, R0ref

        ! Specify host and bubble components
        if (bubbles_euler) then
          id_host = 1
          if (num_fluids == 1) then
            id_bubbles = num_fluids + 1
          else
            id_bubbles = num_fluids
          end if
        else if (bubbles_lagrange) then
          id_bubbles = num_fluids
          id_host = num_fluids - 1
        end if

        ! Reference values
        rho0 = bub_refs%rho0
        x0 = bub_refs%x0
        p0 = bub_refs%p0
        u0 = bub_refs%u0
        T0 = bub_refs%T0
        rhob0 = bub_refs%rhob0
        p0eq = bub_refs%p0eq
        R0ref = bub_refs%R0ref
        ub0 = bub_refs%ub0

        ! Input quantities
        pv = fluid_pp(id_host)%pv / p0
        if (bub_ss) ss = fluid_pp(id_host)%ss
        if (bub_visc) mul0 = fluid_pp(id_host)%mul0
        if (.not. polytropic) Tw = bub_refs%Thost/T0
        if (bubbles_euler .and. (.not. polytropic)) then
          ! Viscosity
          mu_v = fluid_pp(id_host)%mu_v
          mu_n = fluid_pp(id_bubbles)%mu_v
          ! Specific heat ratio
          gamma_v = fluid_pp(id_host)%gamma_v
          gamma_n = fluid_pp(id_bubbles)%gamma_v
          if (thermal == 2) then
            gamma_m = 1._wp
          else
            gamma_m = gamma_n
          end if
          ! Thermal conductivity
          k_v(:) = fluid_pp(id_host)%k_v*(T0/(x0*rho0*u0*u0*u0))
          k_n(:) = fluid_pp(id_bubbles)%k_v*(T0/(x0*rho0*u0*u0*u0))
          ! Molecular weight
          M_v = fluid_pp(id_host)%M_v
          M_n = fluid_pp(id_bubbles)%M_v
          ! Gas constant
          R_v = R_uni/M_v*(rho0*T0/p0)
          R_n = R_uni/M_n*(rho0*T0/p0)
        end if

#ifdef MFC_SIMULATION
        if (bubbles_lagrange) then
          ! Gas constant
          R_v = (R_uni/fluid_pp(id_bubbles)%M_v)*(T0/(u0*u0))
          R_n = (R_uni/fluid_pp(id_host)%M_v)*(T0/(u0*u0))
          ! Specific heat ratio
          gamma_v = fluid_pp(id_bubbles)%gamma_v
          gamma_n = fluid_pp(id_host)%gamma_v
          ! Thermal conductivity
          k_vl = fluid_pp(id_bubbles)%k_v*(T0/(x0*rho0*u0*u0*u0))
          k_nl = fluid_pp(id_host)%k_v*(T0/(x0*rho0*u0*u0*u0))
          ! Specific heat capacity
          cp_v = fluid_pp(id_bubbles)%cp_v*(T0/(u0*u0))
          cp_n = fluid_pp(id_host)%cp_v*(T0/(u0*u0))
        end if
#endif

        ! Nondimensional numbers
        Eu = p0eq/p0
        Ca = Eu - pv
        if (bub_ss) Web = (rho0*x0*u0*u0)/ss
        if (bub_visc) Re_inv = mul0/(rho0*x0*u0)
        if (.not. polytropic) Pe_c = (u0*x0)/fluid_pp(id_host)%D

        if (bubbles_euler) then
          ! Initialize variables for non-polytropic (Preston) model
          if (.not. polytropic) then
              call s_initialize_nonpoly()
          end if
          ! Initialize pb based on surface tension for qbmm (polytropic)
          if (qbmm .and. polytropic) then
              pb0 = Eu
              if (bub_ss) then
                pb0 = pb0 + 2._wp/Web/R0
              end if
          end if
        end if

    end subroutine s_initialize_bubble_vars

    !> Initializes non-polydisperse bubble modeling
    impure subroutine s_initialize_nonpoly()
        integer :: ir
        real(wp), dimension(nb) :: chi_vw0, cp_m0, k_m0, rho_m0, x_vw, omegaN, rhol0

        real(wp), parameter :: k_poly = 1._wp !<
            !! polytropic index used to compute isothermal natural frequency

        ! phi_vn & phi_nv (phi_nn = phi_vv = 1) (Eq. 2.22 in Ando 2010)
        phi_vn = (1._wp + sqrt(mu_v/mu_n)*(M_n/M_v)**(0.25_wp))**2 &
                 /(sqrt(8._wp)*sqrt(1._wp + M_v/M_n))
        phi_nv = (1._wp + sqrt(mu_n/mu_v)*(M_v/M_n)**(0.25_wp))**2 &
                 /(sqrt(8._wp)*sqrt(1._wp + M_n/M_v))

        ! internal bubble pressure 
        pb0 = Eu + 2._wp/Web/R0

        ! mass fraction of vapor (Eq. 2.19 in Ando 2010)
        chi_vw0 = 1._wp/(1._wp + R_v/R_n*(pb0/pv - 1._wp))

        ! specific heat for gas/vapor mixture 
        cp_m0 = chi_vw0*R_v*gamma_v/(gamma_v - 1._wp) &
                + (1._wp - chi_vw0)*R_n*gamma_n/(gamma_n - 1._wp)

        ! mole fraction of vapor (Eq. 2.23 in Ando 2010)
        x_vw = M_n*chi_vw0/(M_v + (M_n - M_v)*chi_vw0)

        ! thermal conductivity for gas/vapor mixture (Eq. 2.21 in Ando 2010)
        k_m0 = x_vw*k_v/(x_vw + (1._wp - x_vw)*phi_vn) &
               + (1._wp - x_vw)*k_n/(x_vw*phi_nv + 1._wp - x_vw)
        k_n(:) = k_n(:)/k_m0(:)
        k_v(:) = k_v(:)/k_m0(:)

        ! mixture density (Eq. 2.20 in Ando 2010)
        rho_m0 = pv/(chi_vw0*R_v*Tw)

        ! mass of gas/vapor
        mass_n0(:) = (4._wp*pi/3._wp)*(pb0(:) - pv)/(R_n*Tw)*R0(:)**3
        mass_v0(:) = (4._wp*pi/3._wp)*pv/(R_v*Tw)*R0(:)**3
        
        ! Peclet numbers (u0 = x0 = 1, effectively, as others are already nondimensionalized using u0 and x0)
        Pe_T(:) = rho_m0*cp_m0(:)/k_m0(:)
        
        ! natural frequencies (Eq. B.1)
        rhol0 = bub_refs%rhob0/bub_refs%rho0
        omegaN(:) = sqrt(3._wp*k_poly*Ca + 2._wp*(3._wp*k_poly - 1._wp)/(Web*R0))/R0/sqrt(rhol0)
        do ir = 1, Nb
            call s_transcoeff(omegaN(ir)*R0(ir), Pe_T(ir)*R0(ir), &
                              Re_trans_T(ir), Im_trans_T(ir))
            call s_transcoeff(omegaN(ir)*R0(ir), Pe_c*R0(ir), &
                              Re_trans_c(ir), Im_trans_c(ir))
        end do
        Im_trans_T = 0._wp

    end subroutine s_initialize_nonpoly

    !> Computes the transfer coefficient for the non-polytropic bubble compression process
        !! @param omega natural frequencies
        !! @param peclet Peclet number
        !! @param Re_trans Real part of the transport coefficients
        !! @param Im_trans Imaginary part of the transport coefficients
    pure elemental subroutine s_transcoeff(omega, peclet, Re_trans, Im_trans)

        real(wp), intent(in) :: omega, peclet
        real(wp), intent(out) :: Re_trans, Im_trans

        complex(wp) :: imag, trans, c1, c2, c3

        imag = (0._wp, 1._wp)

        c1 = imag*omega*peclet
        c2 = sqrt(c1)
        c3 = (exp(c2) - exp(-c2))/(exp(c2) + exp(-c2)) ! TANH(c2)
        trans = ((c2/c3 - 1._wp)**(-1) - 3._wp/c1)**(-1) ! transfer function

        Re_trans = trans
        Im_trans = aimag(trans)

    end subroutine s_transcoeff

    pure elemental subroutine s_int_to_str(i, res)

        integer, intent(in) :: i
        character(len=*), intent(inout) :: res

        write (res, '(I0)') i
        res = trim(res)
    end subroutine s_int_to_str

    !> Computes the Simpson weights for quadrature
    subroutine s_simpson(local_weight, local_R0)

        real(wp), dimension(:), intent(inout) :: local_weight
        real(wp), dimension(:), intent(inout) :: local_R0

        integer :: ir
        real(wp) :: R0mn, R0mx, dphi, tmp, sd
        real(wp), dimension(nb) :: phi

        sd = poly_sigma
        R0mn = 0.8_wp*exp(-2.8_wp*sd)
        R0mx = 0.2_wp*exp(9.5_wp*sd) + 1._wp

        ! phi = ln( R0 ) & return R0
        do ir = 1, nb
            phi(ir) = log(R0mn) &
                      + (ir - 1._wp)*log(R0mx/R0mn)/(nb - 1._wp)
            local_R0(ir) = exp(phi(ir))
        end do
        dphi = phi(2) - phi(1)

        ! weights for quadrature using Simpson's rule
        do ir = 2, nb - 1
            ! Gaussian
            tmp = exp(-0.5_wp*(phi(ir)/sd)**2)/sqrt(2._wp*pi)/sd
            if (mod(ir, 2) == 0) then
                local_weight(ir) = tmp*4._wp*dphi/3._wp
            else
                local_weight(ir) = tmp*2._wp*dphi/3._wp
            end if
        end do
        tmp = exp(-0.5_wp*(phi(1)/sd)**2)/sqrt(2._wp*pi)/sd
        local_weight(1) = tmp*dphi/3._wp
        tmp = exp(-0.5_wp*(phi(nb)/sd)**2)/sqrt(2._wp*pi)/sd
        local_weight(nb) = tmp*dphi/3._wp
    end subroutine s_simpson

    !> This procedure computes the cross product of two vectors.
    !! @param a First vector.
    !! @param b Second vector.
    !! @return The cross product of the two vectors.
    pure function f_cross(a, b) result(c)

        real(wp), dimension(3), intent(in) :: a, b
        real(wp), dimension(3) :: c

        c(1) = a(2)*b(3) - a(3)*b(2)
        c(2) = a(3)*b(1) - a(1)*b(3)
        c(3) = a(1)*b(2) - a(2)*b(1)
    end function f_cross

    !> This procedure swaps two real numbers.
    !! @param lhs Left-hand side.
    !! @param rhs Right-hand side.
    pure elemental subroutine s_swap(lhs, rhs)

        real(wp), intent(inout) :: lhs, rhs
        real(wp) :: ltemp

        ltemp = lhs
        lhs = rhs
        rhs = ltemp
    end subroutine s_swap

    !> This procedure creates a transformation matrix.
    !! @param  p Parameters for the transformation.
    !! @return Transformation matrix.
    pure function f_create_transform_matrix(param, center) result(out_matrix)

        type(ic_model_parameters), intent(in) :: param
        real(wp), dimension(1:3), optional, intent(in) :: center
        real(wp), dimension(1:4, 1:4) :: sc, rz, rx, ry, tr, t_back, t_to_origin, out_matrix

        sc = transpose(reshape([ &
                               param%scale(1), 0._wp, 0._wp, 0._wp, &
                               0._wp, param%scale(2), 0._wp, 0._wp, &
                               0._wp, 0._wp, param%scale(3), 0._wp, &
                               0._wp, 0._wp, 0._wp, 1._wp], shape(sc)))

        rz = transpose(reshape([ &
                               cos(param%rotate(3)), -sin(param%rotate(3)), 0._wp, 0._wp, &
                               sin(param%rotate(3)), cos(param%rotate(3)), 0._wp, 0._wp, &
                               0._wp, 0._wp, 1._wp, 0._wp, &
                               0._wp, 0._wp, 0._wp, 1._wp], shape(rz)))

        rx = transpose(reshape([ &
                               1._wp, 0._wp, 0._wp, 0._wp, &
                               0._wp, cos(param%rotate(1)), -sin(param%rotate(1)), 0._wp, &
                               0._wp, sin(param%rotate(1)), cos(param%rotate(1)), 0._wp, &
                               0._wp, 0._wp, 0._wp, 1._wp], shape(rx)))

        ry = transpose(reshape([ &
                               cos(param%rotate(2)), 0._wp, sin(param%rotate(2)), 0._wp, &
                               0._wp, 1._wp, 0._wp, 0._wp, &
                               -sin(param%rotate(2)), 0._wp, cos(param%rotate(2)), 0._wp, &
                               0._wp, 0._wp, 0._wp, 1._wp], shape(ry)))

        tr = transpose(reshape([ &
                               1._wp, 0._wp, 0._wp, param%translate(1), &
                               0._wp, 1._wp, 0._wp, param%translate(2), &
                               0._wp, 0._wp, 1._wp, param%translate(3), &
                               0._wp, 0._wp, 0._wp, 1._wp], shape(tr)))

        if (present(center)) then
            ! Translation matrix to move center to the origin
            t_to_origin = transpose(reshape([ &
                                            1._wp, 0._wp, 0._wp, -center(1), &
                                            0._wp, 1._wp, 0._wp, -center(2), &
                                            0._wp, 0._wp, 1._wp, -center(3), &
                                            0._wp, 0._wp, 0._wp, 1._wp], shape(tr)))

            ! Translation matrix to move center back to original position
            t_back = transpose(reshape([ &
                                       1._wp, 0._wp, 0._wp, center(1), &
                                       0._wp, 1._wp, 0._wp, center(2), &
                                       0._wp, 0._wp, 1._wp, center(3), &
                                       0._wp, 0._wp, 0._wp, 1._wp], shape(tr)))

            out_matrix = matmul(tr, matmul(t_back, matmul(ry, matmul(rx, matmul(rz, matmul(sc, t_to_origin))))))
        else
            out_matrix = matmul(ry, matmul(rx, rz))
        end if

    end function f_create_transform_matrix

    !> This procedure transforms a vector by a matrix.
    !! @param vec Vector to transform.
    !! @param matrix Transformation matrix.
    pure subroutine s_transform_vec(vec, matrix)

        real(wp), dimension(1:3), intent(inout) :: vec
        real(wp), dimension(1:4, 1:4), intent(in) :: matrix

        real(wp), dimension(1:4) :: tmp

        tmp = matmul(matrix, [vec(1), vec(2), vec(3), 1._wp])
        vec = tmp(1:3)

    end subroutine s_transform_vec

    !> This procedure transforms a triangle by a matrix, one vertex at a time.
    !! @param triangle Triangle to transform.
    !! @param matrix   Transformation matrix.
    pure subroutine s_transform_triangle(triangle, matrix, matrix_n)

        type(t_triangle), intent(inout) :: triangle
        real(wp), dimension(1:4, 1:4), intent(in) :: matrix, matrix_n

        integer :: i

        do i = 1, 3
            call s_transform_vec(triangle%v(i, :), matrix)
        end do

        call s_transform_vec(triangle%n(1:3), matrix_n)

    end subroutine s_transform_triangle

    !> This procedure transforms a model by a matrix, one triangle at a time.
    !! @param model  Model to transform.
    !! @param matrix Transformation matrix.
    pure subroutine s_transform_model(model, matrix, matrix_n)

        type(t_model), intent(inout) :: model
        real(wp), dimension(1:4, 1:4), intent(in) :: matrix, matrix_n

        integer :: i

        do i = 1, size(model%trs)
            call s_transform_triangle(model%trs(i), matrix, matrix_n)
        end do

    end subroutine s_transform_model

    !> This procedure creates a bounding box for a model.
    !! @param model Model to create bounding box for.
    !! @return Bounding box.
    pure function f_create_bbox(model) result(bbox)

        type(t_model), intent(in) :: model
        type(t_bbox) :: bbox

        integer :: i, j

        if (size(model%trs) == 0) then
            bbox%min = 0._wp
            bbox%max = 0._wp
            return
        end if

        bbox%min = model%trs(1)%v(1, :)
        bbox%max = model%trs(1)%v(1, :)

        do i = 1, size(model%trs)
            do j = 1, 3
                bbox%min = min(bbox%min, model%trs(i)%v(j, :))
                bbox%max = max(bbox%max, model%trs(i)%v(j, :))
            end do
        end do

    end function f_create_bbox

    !> This procedure performs xor on lhs and rhs.
    !! @param lhs logical input.
    !! @param rhs other logical input.
    !! @return xored result.
    pure elemental function f_xor(lhs, rhs) result(res)

        logical, intent(in) :: lhs, rhs
        logical :: res

        res = (lhs .and. .not. rhs) .or. (.not. lhs .and. rhs)
    end function f_xor

    !> This procedure converts logical to 1 or 0.
    !! @param perdicate A Logical argument.
    !! @return 1 if .true., 0 if .false..
    pure elemental function f_logical_to_int(predicate) result(int)

        logical, intent(in) :: predicate
        integer :: int

        if (predicate) then
            int = 1
        else
            int = 0
        end if
    end function f_logical_to_int

    !> This function generates the unassociated legendre poynomials
    !! @param x is the input value
    !! @param l is the degree
    !! @return P is the unassociated legendre polynomial evaluated at x
    pure recursive function unassociated_legendre(x, l) result(result_P)

        integer, intent(in) :: l
        real(wp), intent(in) :: x
        real(wp) :: result_P

        if (l == 0) then
            result_P = 1._wp
        else if (l == 1) then
            result_P = x
        else
            result_P = ((2*l - 1)*x*unassociated_legendre(x, l - 1) - (l - 1)*unassociated_legendre(x, l - 2))/l
        end if

    end function unassociated_legendre

    !> This function calculates the spherical harmonic function evaluated at x and phi
    !! @param x is the x coordinate
    !! @param phi is the phi coordinate
    !! @param l is the degree
    !! @param m_order is the order
    !! @return Y is the spherical harmonic function evaluated at x and phi
    pure recursive function spherical_harmonic_func(x, phi, l, m_order) result(Y)

        integer, intent(in) :: l, m_order
        real(wp), intent(in) :: x, phi
        real(wp) :: Y, prefactor, local_pi

        local_pi = acos(-1._wp)
        prefactor = sqrt((2*l + 1)/(4*local_pi)*factorial(l - m_order)/factorial(l + m_order)); 
        if (m_order == 0) then
            Y = prefactor*associated_legendre(x, l, m_order); 
        elseif (m_order > 0) then
            Y = (-1._wp)**m_order*sqrt(2._wp)*prefactor*associated_legendre(x, l, m_order)*cos(m_order*phi); 
        end if

    end function spherical_harmonic_func

    !> This function generates the associated legendre polynomials evaluated
    !! at x with inputs l and m
    !! @param x is the input value
    !! @param l is the degree
    !! @param m_order is the order
    !! @return P is the associated legendre polynomial evaluated at x
    pure recursive function associated_legendre(x, l, m_order) result(result_P)

        integer, intent(in) :: l, m_order
        real(wp), intent(in) :: x
        real(wp) :: result_P

        if (m_order <= 0 .and. l <= 0) then
            result_P = 1; 
        elseif (l == 1 .and. m_order <= 0) then
            result_P = x; 
        elseif (l == 1 .and. m_order == 1) then
            result_P = -(1 - x**2)**(1._wp/2._wp); 
        elseif (m_order == l) then
            result_P = (-1)**l*double_factorial(2*l - 1)*(1 - x**2)**(l/2); 
        elseif (m_order == l - 1) then
            result_P = x*(2*l - 1)*associated_legendre(x, l - 1, l - 1); 
        else
            result_P = ((2*l - 1)*x*associated_legendre(x, l - 1, m_order) - (l + m_order - 1)*associated_legendre(x, l - 2, m_order))/(l - m_order); 
        end if

    end function associated_legendre

    !> This function calculates the double factorial value of an integer
    !! @param n_in is the input integer
    !! @return R is the double factorial value of n
    pure elemental function double_factorial(n_in) result(R_result)

        integer, intent(in) :: n_in
        integer, parameter :: int64_kind = selected_int_kind(18) ! 18 bytes for 64-bit integer
        integer(kind=int64_kind) :: R_result
        integer :: i

        R_result = product((/(i, i=n_in, 1, -2)/))

    end function double_factorial

    !> The following function calculates the factorial value of an integer
    !! @param n_in is the input integer
    !! @return R is the factorial value of n
    pure elemental function factorial(n_in) result(R_result)

        integer, intent(in) :: n_in
        integer, parameter :: int64_kind = selected_int_kind(18) ! 18 bytes for 64-bit integer
        integer(kind=int64_kind) :: R_result

        integer :: i

        R_result = product((/(i, i=n_in, 1, -1)/))

    end function factorial

    !> This function calculates a smooth cut-on function that is zero for x values
    !! smaller than zero and goes to one. It can be used for generating smooth
    !! initial conditions
    !! @param x is the input value
    !! @param eps is the smoothing parameter
    !! @return fx is the cut-on function evaluated at x
    function f_cut_on(x, eps) result(fx)

        real(wp), intent(in) :: x, eps
        real(wp) :: fx

        fx = 1 - f_gx(x/eps)/(f_gx(x/eps) + f_gx(1 - x/eps))

    end function f_cut_on

    !> This function calculates a smooth cut-off function that is one for x values
    !! smaller than zero and goes to zero. It can be used for generating smooth
    !! initial conditions
    !! @param x is the input value
    !! @param eps is the smoothing parameter
    !! @return fx is the cut-ff function evaluated at x
    function f_cut_off(x, eps) result(fx)

        real(wp), intent(in) :: x, eps
        real(wp) :: fx

        fx = f_gx(x/eps)/(f_gx(x/eps) + f_gx(1 - x/eps))

    end function f_cut_off

    !> This function is a helper function for the functions f_cut_on and f_cut_off
    !! @param x is the input value
    !! @return gx is the result
    function f_gx(x) result(gx)

        real(wp), intent(in) :: x
        real(wp) :: gx

        if (x > 0) then
            gx = exp(-1._wp/x)
        else
            gx = 0._wp
        end if

    end function f_gx

end module m_helper
